%{
  #include "tabla-simbolos.h"
  // c√≥digo C
  #define YYERROR_VERBOSE 1
  int yylex(void);
  void yyerror(const char *s);
  int yydebug = 0;
%}

%token CABECERA_PROGRAMA
%token LLAVE_IZQ
%token LLAVE_DER
%token CORCHETE_IZQ
%token CORCHETE_DER
%token PARENTESIS_IZQ
%token PARENTESIS_DER
%token TOKEN_SUBPROGRAMA
%token INICIO_DECLARACION_VARIABLES
%token FIN_DECLARACION_VARIABLES
%token TIPO
%token NOMBRE
%token PYC
%token IF
%token ELSE
%token WHILE
%token FOR
%token DIRECCION_FOR
%token INIT_FOR
%token DO
%token SCANF
%token PRINTF
%token RETURN
%token EQUALS
%token NATURAL
%token FL_BOOL_CH
%token CADENA

%left OP_OR
%left OP_AND
%left OP_EQ
%left OP_CMP
%left PLUS_MINUS
%left OP_MULT_MAT
%left OP_MULT
%right NOT
%right COMA

%start PROGRAMA


%%


PROGRAMA : CABECERA_PROGRAMA BLOQUE
  ;

BLOQUE :
    INICIO_DE_BLOQUE               { TS_insertar_marca(); }
    DECLARACION_VARIABLES_LOCALES
    DECLARACION_SUBPROGRAMAS
    SENTENCIAS
    FIN_DE_BLOQUE                  { TS_fin_bloque(); }
  ;

DECLARACION_VARIABLES_LOCALES :
    INICIO_DECLARACION_VARIABLES
    VARIABLES_LOCALES
    FIN_DECLARACION_VARIABLES
  |
  ;

VARIABLE_PYC : VARIABLE_LOCAL PYC
  | error
  ;  

VARIABLES_LOCALES : VARIABLE_PYC VARIABLES_LOCALES
  | VARIABLE_LOCAL
  ;

VARIABLE_LOCAL : TIPO LISTA_IDENTIFICADOR
  | error
  ;

LISTA_IDENTIFICADOR : IDENTIFICADOR        { TS_insertar_identificador($1); }
  | IDENTIFICADOR COMA LISTA_IDENTIFICADOR { TS_insertar_identificador($1); }
  | error
  ;

IDENTIFICADOR : NOMBRE
  | NOMBRE CORCHETE_IZQ DIMENSIONES CORCHETE_DER
  ;

DIMENSIONES : NATURAL    { TS_dimension($1); }
  | NATURAL COMA NATURAL { TS_dimension($1); TS_dimension($3); }
  ;

DECLARACION_SUBPROGRAMAS : DECLARACION_SUBPROGRAMAS DECLARACION_SUBPROGRAMA
  |
  ;

DECLARACION_SUBPROGRAMA : CABECERA_SUBPROGRAMA BLOQUE
  ;

CABECERA_SUBPROGRAMA : TOKEN_SUBPROGRAMA
                       NOMBRE { TS_insertar_procedimiento($2); }
                       PARENTESIS_IZQ PARAMETROS_PROCEDIMIENTO PARENTESIS_DER
  ;

INICIO_DE_BLOQUE : LLAVE_IZQ
  ;

FIN_DE_BLOQUE : LLAVE_DER
  ;

PARAMETRO : TIPO IDENTIFICADOR { TS_insertar_parametro($2); }
  | error
  ;

LISTA_PARAMETROS : PARAMETRO
  | PARAMETRO COMA LISTA_PARAMETROS
  ;

PARAMETROS_PROCEDIMIENTO : LISTA_PARAMETROS
  |
  ;

SENTENCIAS : SENTENCIAS SENTENCIA
  |
  ;

SENTENCIA : BLOQUE
  | SENTENCIA_ASIGNACION
  | SENTENCIA_IF
  | SENTENCIA_WHILE
  | SENTENCIA_ENTRADA
  | SENTENCIA_SALIDA
  | LLAMADA_PROCED
  | SENTENCIA_FOR
  | SENTENCIA_RETURN
  ;

SENTENCIA_ASIGNACION : IDENTIFICADOR_EXPR EQUALS EXPR PYC
  ;

SENTENCIA_IF : IF PARENTESIS_IZQ EXPR PARENTESIS_DER SENTENCIA SENTENCIA_ELSE
  ;

SENTENCIA_ELSE : ELSE SENTENCIA
  |
  ;

SENTENCIA_WHILE : WHILE PARENTESIS_IZQ EXPR PARENTESIS_DER SENTENCIA
  ;

SENTENCIA_FOR : FOR NOMBRE INIT_FOR EXPR DIRECCION_FOR EXPR DO SENTENCIA
  ;

SENTENCIA_ENTRADA : SCANF LISTA_IDENTIFICADOR_EXPR PYC
  ;

SENTENCIA_SALIDA : PRINTF LISTA_EXPRESIONES_O_CADENA PYC
  ;

LISTA_EXPRESIONES_O_CADENA : EXPR_O_CADENA
  | EXPR_O_CADENA COMA LISTA_EXPRESIONES_O_CADENA
  ;

EXPR_O_CADENA : EXPR
  | CADENA
  ;

SENTENCIA_RETURN : RETURN PYC
  ;

ARGUMENTOS_PROCEDIMIENTO : LISTA_EXPR
  |
  ;

LLAMADA_PROCED : NOMBRE PARENTESIS_IZQ ARGUMENTOS_PROCEDIMIENTO PARENTESIS_DER PYC
  ;

EXPR : PARENTESIS_IZQ EXPR PARENTESIS_DER
         | NOT EXPR
         | PLUS_MINUS EXPR
         | EXPR PLUS_MINUS EXPR
         | EXPR OP_OR EXPR
         | EXPR OP_AND EXPR
         | EXPR OP_EQ EXPR
         | EXPR OP_CMP EXPR
         | EXPR OP_MULT EXPR
         | EXPR OP_MULT_MAT EXPR
         | IDENTIFICADOR_EXPR
         | FL_BOOL_CH
         | NATURAL
         | VECTOR
         | error
  ;

IDENTIFICADOR_EXPR : NOMBRE
  | NOMBRE CORCHETE_IZQ EXPR CORCHETE_DER
  | NOMBRE CORCHETE_IZQ EXPR COMA EXPR CORCHETE_DER
  ;

LISTA_IDENTIFICADOR_EXPR : IDENTIFICADOR_EXPR
  | IDENTIFICADOR_EXPR COMA LISTA_IDENTIFICADOR_EXPR
  ;

LISTA_EXPR : EXPR COMA LISTA_EXPR
  | EXPR
  ;

VECTOR : LLAVE_IZQ LISTA_EXPR LLAVE_DER
  ;

%%

#include "tokenizador.c"

void yyerror(const char *s) {
    printf(
           "Yacc error at line %d: %s. Unexpected \"%s\"\n",
           yylineno,
           s,
           yytext);
}

int main(){
  yyparse();
}
